# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("faceauthservice_config") {
  include_dirs = [
    "inc",
    "../common/inc",
    "../common/logs",
    "../common/utils",
  ]
}

ohos_source_set("faceauthservice_source_set") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    blocklist = "../cfi_blocklist.txt"
  }
  branch_protector_ret = "pac_ret"
  sources = [
    "src/face_auth_all_in_one_executor_hdi.cpp",
    "src/face_auth_driver_hdi.cpp",
    "src/face_auth_executor_callback_hdi.cpp",
    "src/face_auth_interface_adapter.cpp",
    "src/face_auth_service.cpp",
    "src/sa_command_manager.cpp",
    "src/screen_brightness_manager.cpp",
    "src/service_ex_manager.cpp",
  ]
  include_dirs = [
    "inc",
    "../common/inc",
    "../common/logs",
    "../common/utils",
  ]

  public_configs = [ ":faceauthservice_config" ]

  deps = [ "../frameworks/ipc:faceauth_framework_stub" ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "drivers_interface_camera:libbuffer_producer_sequenceable_1.0",
    "drivers_interface_face_auth:libface_auth_proxy_2.0",
    "hilog:libhilog",
    "ipc:ipc_single",
    "miscdevice:vibrator_interface_native",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "user_auth_framework:userauth_executors",
  ]

  if (defined(global_parts_info.graphic_graphic_2d)) {
    external_deps += [ "graphic_surface:surface" ]
  }

  remove_configs = [ "//build/config/compiler:no_exceptions" ]

  subsystem_name = "useriam"
  part_name = "face_auth"
}

ohos_shared_library("faceauthservice") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    blocklist = "../cfi_blocklist.txt"
  }
  branch_protector_ret = "pac_ret"
  deps = [ ":faceauthservice_source_set" ]

  external_deps = [ "hilog:libhilog" ]

  if (use_musl) {
    version_script = "face_auth_service_map"
  }

  subsystem_name = "useriam"
  part_name = "face_auth"
}
